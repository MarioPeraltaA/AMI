! ====================================
! Three kind of variable loads circuit
! ====================================

clear 

! Define CKT
new circuit.PVtest_ckt BasekV=34.5 pu=1.00 angle=0
~ frequency=60 phases=3 Isc3=2000 Isc1=2100
set defaultbasefrequency=60 ! Good practice always indicate freq.
set EarthModel = Carson     ! to get Z/length lines

! -----
! Lines
! -----
! Call library: LV conductor
redirect WireDataAAC.dss

! Lil library: MV conductors data
new WireData.ACSR_336.4_26STR  Rac=0.306   Runits=mile  GMR=0.02440  GMRunits=ft  DIAM=0.06008 Radunits=ft Normamps=529
new WireData.ACSR_4/0_6STR     Rac=0.592   Runits=mile  GMR=0.00814  GMRunits=ft  DIAM=0.04692 Radunits=ft Normamps=357
// new WireData.ACSR_1/0_6STR     Rac=1.120   Runits=mile 	GMR=0.00446	 GMRunits=ft  DIAM=0.398   Radunits=in Normamps=242

! Spacing
new LineSpacing.3F_MTH nconds=4 nphases=3 units=ft x=[0 2.5 7 4] h=[29 29 29 25]
// new LineSpacing.1F_MTH nconds=2 nphases=1 units=ft x=[0 0.5] h=[30 25]
new LineSpacing.1F_4WIRE nconds=4 nphases=3 units=m x=[0.20 0.20 0.20 0.20] h=[6.7 6.90 7.1 7.30]

! Geometry
new Linegeometry.3FMV336ACSR4/0ACSR_H  nconds=4 nphases=3 spacing=3F_MTH
~ wires=[ACSR_336.4_26STR  ACSR_336.4_26STR  ACSR_336.4_26STR  ACSR_4/0_6STR] Reduce=Y

// new Linegeometry.1FMV1/0ACSR1/0ACSR_M  nconds=2 nphases=1 spacing=1F_MTH
// ~ wires=[ACSR_1/0_6STR  ACSR_1/0_6STR] Reduce=Y

new Linegeometry.1FLV3/0AAC3/0AAC_P nconds=4 nphases=3 spacing=1F_4WIRE
~ wires=[AAC_3/0_7STR AAC_3/0_7STR AAC_3/0_7STR AAC_3/0_7STR]  Reduce=Y


! muestra las impedancias y capacitancias de la l√≠nea
// show lineconstants [60] [mile]

! Define lines
new line.linea3FMV bus1=sourcebus.1.2.3 bus2=busT.1.2.3
~ geometry=3FMV336ACSR4/0ACSR_H length=200 units=m

new line.lineLV1 bus1=busA.1.2.3 bus2=busB.1.2.3
~ geometry=1FLV3/0AAC3/0AAC_P length=25 units=m

new line.lineLV2 bus1=busB.1.2.3 bus2=busC.1.2.3
~ geometry=1FLV3/0AAC3/0AAC_P length=50 units=m

! ------------
! Transformers
! ------------
! Transformer 3F, 2w, wye-wye
new transformer.Tx3F phases=3 windings=2 %noloadloss=0.35 %imag=1.00
~ buses=[busT.1.2.3 busA.1.2.3] conns=[wye, wye]
~ kvs=[34.5 0.240] kvas=[300 300] Xhl=5.32 %Rs=[0.70 0.70]

! -----
! Loads
! -----
! Shapes
new Loadshape.loadshape_1 npts=144 minterval=10 mult=(file=loadshape_1.txt) useactual=no
new Loadshape.loadshape_2 npts=144 minterval=10 mult=(file=loadshape_2.txt) useactual=no
new Loadshape.loadshape_3 npts=144 minterval=10 mult=(file=loadshape_3.txt) useactual=no
new load.cliente_1 bus1=busB.1 kV=0.1386 model=1 conn=wye kW=3 pf=0.98 status=variable daily=loadshape_1 phases=1
new load.cliente_2 bus1=busB.2 kV=0.1386 model=1 conn=wye kW=4 pf=0.98 status=variable daily=loadshape_2 phases=1
new load.cliente_3 bus1=busB.3 kV=0.1386 model=1 conn=wye kW=4 pf=0.96 status=variable daily=loadshape_3 phases=1

! --------
! PVSystem
! --------
! Import
Redirect PVsys.dss

! ----------
! Simulation
! ----------
! To p.u.
Set VoltageBases = [34.5, 0.240]    ! kV
CalcVoltageBases

! Monitors
// new monitor.subestacionPQ element=vsource.source terminal=1 mode=1 ppolar=no   ! Source
new monitor.LV_PQline1 Element=line.lineLV1 terminal=1 mode=1 ppolar=no        ! Source line
new monitor.PQ_PVsys Element=PVsystem.PV terminal=1 mode=1 ppolar=no           ! PVsys contribution

new monitor.LV_PQload1 Element=load.cliente_1 Terminal=1 Mode=1 ppolar=no
new monitor.LV_VIload1 Element=load.cliente_1 Terminal=1 Mode=0

new monitor.LV_PQload2 Element=load.cliente_2 Terminal=1 Mode=1 ppolar=no
new monitor.LV_VIload2 Element=load.cliente_2 Terminal=1 Mode=0

new monitor.LV_PQload3 Element=load.cliente_3 Terminal=1 Mode=1 ppolar=no
new monitor.LV_VIload3 Element=load.cliente_3 Terminal=1 Mode=0

! Kind of simulation: daily
set mode=daily stepsize=10m number=144

! Simulate
solve

! ----
! Data
! ----
! Retrieve
export monitor LV_PQline1
export monitor PQ_PVsys

export monitor LV_PQload1
export monitor LV_VIload1

export monitor LV_PQload2
export monitor LV_VIload2

export monitor LV_PQload3
export monitor LV_VIload3

! Results
// show voltages LN Nodes
! Show power through buses and lines
// show powers kva elements
